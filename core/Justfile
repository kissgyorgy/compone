pytest := "python -m pytest tests/"

help:
    @just --list

# Run linters and other checks
check:
    ruff check .

test-versions *tox-params:
    tox {{ tox-params }}

# Run tests with pytest
test *pytest-params:
    {{ pytest }} {{ pytest-params }}

# Run tests continuously and re-run them with watchexec if files changed
watchtests *pytest-params:
    watchexec --restart --exts py --stop-timeout 0 -- {{ pytest }} {{ pytest-params }}

testpypi-release: test
    #!/usr/bin/env bash
    export POETRY_PYPI_TOKEN_TESTPYPI=$(op.exe read op://Secrets/test-pypi-token/credential)
    export POETRY_REPOSITORIES_TESTPYPI_URL=https://test.pypi.org/legacy/
    poetry publish --build --repository testpypi

release: test
    #!/usr/bin/env bash
    export POETRY_PYPI_TOKEN_PYPI=$(op.exe read op://Secrets/pypi-token/credential)
    poetry publish --build
